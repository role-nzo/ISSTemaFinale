System coldstorageservice

Dispatch coapUpdate: coapUpdate(RESOURCE, VALUE)
Event alarm           : alarm(X)

Request engage        : engage(OWNER)
Reply   engagedone    : engagedone(ARG)
Reply   engagerefused : engagerefused(ARG)

Dispatch disengage    : disengage(ARG)

Request moverobot    :  moverobot(TARGETX, TARGETY)
Reply moverobotdone  :  moverobotok(ARG)
Reply moverobotfailed:  moverobotfailed(ARG)


Dispatch goMoveToIndoor   : goMoveToIndoor(_)
Dispatch goMoveToColdRoom : goMoveToColdRoom(_)
Dispatch goMoveToHome     : goMoveToHome(_)


//Ottenimento del ticket
Request newticket		: newticket( FW )
Reply newticketaccepted : newticketaccepted( TICKET )
Reply newticketrefused  : newticketrefused( REASON )


//Show ticket per rilascio merce
Request storefood 		: storefood( TICKET, FW )
Reply storefoodaccepted : storefoodaccepted( MESSAGE )
Reply storefoodrejected : storefoodrejected( REASON )

Request loaddone	: loaddone(FW)
Reply chargetaken	: chargetaken(MESSAGE)
Reply chargefailure : chargefailure(REASON)


Request waitLoad : waitLoad(_)
Reply waitLoadDone : waitLoadDone(_)

Dispatch depositdone : depositdone(_)
Dispatch updatevirtualweight : updatevirtualweight(FW)

Request clearColdRoom : clearColdRoom(_)
Reply coldRoomCleared : coldRoomCleared(_)

//invio mappa 
Dispatch sendmap 	: sendmap(_)

//sonar e led
//Eventi sonar
Dispatch stopevent : stopevent(_)
Dispatch resumevent : resumevent(_)

//Per immagine requisiti
//Dispatch ledstatuschange : ledstatuschange(STATUS)

Context ctxbasicrobot 		  ip [host="192.168.1.141" port=8020] 
Context ctxcoldstorageservice ip [host="localhost" port=8022]
Context ctxraspberry 		  ip [host="192.168.1.35" port=8023]
Context ctxserviceaccessgui 		  ip [host="192.168.1.35" port=8023]
Context ctxservicestatusgui 		  ip [host="192.168.1.35" port=8023]

ExternalQActor basicrobot context ctxbasicrobot
ExternalQActor robotposendosimbiotico context ctxbasicrobot
ExternalQActor led context ctxraspberry
ExternalQActor sonar context ctxraspberry

//For image
/*QActor led context ctxraspberry{
	State s0 initial{
		
	}
}

QActor sonar context ctxraspberry{
	State s0 initial{
		forward transporttrolley -m stopevent:stopevent(_)
		forward transporttrolley -m resumevent:resumevent(_) 
	}
}*/

QActor transporttrolley context ctxcoldstorageservice{
	[#
		var GoingTo = " "
		var LastStopTime = 0L
		var Mint = 5000
		var AlarmCondition = false
		var MoveAlarm = " "
		var Stopped = false
		var Limit = 10
	#]
	State s0 initial {
		println("transporttrolley starts") color blue
		//Acquisizione del basicrobot
    	request basicrobot -m engage : engage(transporttrolley,330)
	}
	Transition t0 whenReply engagedone    -> init
                  whenReply engagerefused -> waitrobotfree
                  
    State waitrobotfree{
		println("transporttrolley engage refused") color blue
	}
	
	State init{
		println("transporttrolley init") color blue
		//[# subscribeToLocalActor("sonarhandler")#] 
	} Goto waitcoldstorageservicerequest
	
	
	State waitcoldstorageservicerequest {
	  println("$name | waiting the client request...") color blue
	}
	Transition t0 whenMsg goMoveToIndoor   -> elabMoveIndoor
				  whenMsg goMoveToColdRoom -> elabMoveColdRoom
				  whenMsg goMoveToHome     -> elabMoveHome
				  //whenMsg resumevent 	   -> resuming
	 
	State elabMoveIndoor {	
		printCurrentMessage color blue	
		[# GoingTo = "INDOOR"#]
		request basicrobot -m moverobot	: moverobot(0, 4)
	}
	Goto waitRobot
	
	State elabMoveColdRoom {
		printCurrentMessage color blue
        [# GoingTo = "COLDROOM"#]
        request basicrobot -m moverobot	: moverobot(4, 3)	
	}
	Goto waitRobot
	
	State elabMoveHome {
		printCurrentMessage color blue
		[# GoingTo = "HOME"#]
        request basicrobot -m moverobot	: moverobot(0, 0)		
	}
	Goto waitRobot
	
	State waitRobot{
		println("Wait robot")
		//forward led -m ledstatuschange:ledstatuschange(_)
	}
	Transition t1 whenMsg stopevent -> checkSonarData
				  whenReply moverobotfailed -> robothit
				  whenReply moverobotdone -> planFinishSwitch
				  whenMsg resumevent -> waitRobot
	
	State robothit{
		println("Robot hit")
		updateResource [# "transporttrolleystatus(stopped)"#]
	}
	Goto waitcoldstorageservicerequest
		  
	State planFinishSwitch{
		
	}Goto planFinishIndoor if [#GoingTo == "INDOOR"#] else planFinishSecondSwitch
	
	State planFinishSecondSwitch{
		
	}Goto planFinishColdRoom if [#GoingTo == "COLDROOM"#] else planFinishHome
	
	State checkSonarData {
		[#
		   val currentTime = java.time.Instant.now().epochSecond
		   AlarmCondition = (currentTime - LastStopTime)*1000 > Mint
		   if(AlarmCondition){#]
		   		
				emit alarm : alarm(stop)
				println("alarm emitted")
				
				
		 [#}
		   else{
		   		#]	   		
		   		println("Stop failed: not enough time from last stop")
		   [#}			
		#]		
	}
	Transition t3 whenReply moverobotfailed -> stopped
				  whenReply moverobotdone -> planFinishSwitch
	
	State stopped{
		println("Sono fermo")
		[# LastStopTime = java.time.Instant.now().epochSecond #]
		updateResource [# "transporttrolleystatus(stopped)"#]
		updateResource [# "robotfree(fermo)"#]
	}
	Transition t2 whenMsg resumevent -> resuming
	
	State resuming {
		println("Resuming")
		[#if(GoingTo == "HOME"){#]
			updateResource [# "robotfree(libero)"#]
		[#}else{#]
			updateResource [# "robotfree(occupato)"#]	
		[#}#]
		
	}Goto elabMoveIndoor if [#GoingTo == "INDOOR"#] else secondResuming
	
	State secondResuming{
		
	}Goto elabMoveColdRoom if [#GoingTo == "COLDROOM"#] else elabMoveHome
						 
	
	State planFinishIndoor {
		//prendere il carico
		
		println("$name | at Indoor") color blue
		delay 3000
	} Transition t1 whenRequest waitLoad -> elabWaitLoad
	
	State planFailedIndoor {
		
		println("$name | at Indoor") color blue
		delay 3000
		
	} Transition t1 whenRequest waitLoad -> elabWaitLoad
	
	State elabWaitLoad {
		replyTo waitLoad with waitLoadDone  : waitLoadDone(valid)
	} Goto elabMoveColdRoom
	
	State planFinishColdRoom {
		//depositare il carico
		
		println("$name | at ColdRoom") color blue
		delay 1500
		forward coldstorageservice -m depositdone : depositdone(0)
		
	} Transition t5 whenTime 4000 -> elabMoveHome
		  			whenMsg goMoveToIndoor  -> elabMoveIndoor
	
	State planFinishHome {
		
		println("$name | at home") color blue
		updateResource [# "transporttrolleystatus(home)"#]
		delay 1000
		
	    
	} Goto waitcoldstorageservicerequest
		
}



QActor statusservice context ctxcoldstorageservice {
	
	[#
		var Map = ""
		var PosX = 0
		var PosY = 0
		var RobotFree = "libero"
		var CurrentWeightReal = 0 
		var RejectedTickets = 0  
	#]
	
	State s0 initial {
		
		//observeResource coldstorageservice
		observeResource transporttrolley
		[#
			//CoapObserverSupport(myself, "localhost","8020","ctxbasicrobot","robotposendosimbiotico")	
			CoapObserverSupport(myself, "192.168.1.141","8020","ctxbasicrobot","robotpos")
			//CoapObserverSupport(myself, "localhost","8022","ctxcoldstorageservice","coldstorageservice")
		#]
		delay 500
		observeResource coldstorageservice
		
		//request basicrobot -m moverobot	: moverobot(0, 0)
		forward transporttrolley -m goMoveToHome : goMoveToHome(0)
	}
	Goto observing
	
	State observing {
		
	} Transition t0 whenMsg coapUpdate -> doObserve
	 				whenMsg sendmap -> sendMap

	
	State sendMap{
		
		updateResource [# "status($Map, $PosX, $PosY, $RobotFree, $CurrentWeightReal, $RejectedTickets)" #]
		
	}Goto observing
	
	State doObserve{
		println("print doObserve")
		printCurrentMessage
		[#
			val msg = currentMsg.msgContent().drop(11).dropLast(1)
			val resource = msg.split(", ")[0]
			val value = msg.dropWhile { it != ',' }.drop(2)
			println("--" + value + "--")			 
			if( resource == "coldstorageservice" || resource == "transporttrolley" ) {
				if( value.startsWith("robotfree") ) {
					RobotFree = value.split("(")[1].dropLast(1)
				} else if( value.startsWith("ticketrejected") ) {
					println("Status " + RejectedTickets)
					RejectedTickets++
				} else if( value.startsWith("weightupdate") ) {
					CurrentWeightReal = value.split("(")[1].dropLast(1).toInt()
				}
			} else if( resource == "robotpos" ) {
				
				if( value.startsWith("|") ) {
					Map = value.substring(0, value.lastIndexOf("\n"))
					var temp = value.substring(value.lastIndexOf("\n")).split(")")[0].split("(")[1]
					PosX = temp.split(",")[0].toInt()
					PosY = temp.split(",")[1].toInt()
				}
			}
		println(" " + PosX + " " + PosY + " " + RobotFree + " " + CurrentWeightReal + " " + RejectedTickets)		 
		#]
		
		updateResource [# "status($PosX, $PosY, $RobotFree, $CurrentWeightReal, $RejectedTickets)" #]
	}
	Goto observing
	
}

QActor emptycoldroom context ctxcoldstorageservice{
	
	State s0 initial{
		
		delay 120000
		request coldstorageservice -m clearColdRoom : clearColdRoom(0)
		println("$name | richiesta svuotamento coldroom")
		
	}Goto s0
		
}

QActor ticketservice context ctxcoldstorageservice {
	
	[# var ticketList = mutableListOf<Ticket>() 
	   var currentWeightVirtual = 0 
	   var maxWeight = 100
	   var TimeMax = 300
	#]
	
	State s0 initial{
		println("$name | wait for request") color yellow
	}
	Transition t0 whenRequest newticket -> elabNewTicket
				 	whenRequest storefood  -> elabStoreFood
				  whenMsg updatevirtualweight -> elabUpdateVirtualWeight
	

	State elabNewTicket{
		
		printCurrentMessage
		onMsg( newticket : newticket( FW )){
			
			[#
			val currentTime = java.time.Instant.now().epochSecond 
			for( t in ticketList){
				if((currentTime - t.creationTime) > TimeMax){
					currentWeightVirtual -= t.fw
					ticketList.remove(t)
				}
			}
			var fw = payloadArg(0).toInt()
			if(currentWeightVirtual + fw <= maxWeight){
				currentWeightVirtual += fw
				var Id = Ticket.getRandomId() 
				var found = false
				while(found){
				found = false
				
					for (t in ticketList){
						if(t.id==Id){
							Id = Ticket.getRandomId()
							found = true
							break
						}
					}
				}
				
				var ticket = Ticket(Id, java.time.Instant.now().epochSecond, fw)
				
				ticketList.add(ticket)
				
			
				#] replyTo newticket with newticketaccepted:newticketaccepted( $Id ) [#
				} else {
				#] replyTo newticket with newticketrefused:newticketrefused( Peso ) [#
				}
			#]
		}
		
	}
	Goto s0
	
	State elabUpdateVirtualWeight{
			onMsg( updatevirtualweight : updatevirtualweight(FW)){
				[#
					currentWeightVirtual = currentWeightVirtual - payloadArg(0).toInt()
				#]
			}
			println("$name | current virtual weight: $currentWeightVirtual") color yellow
	}Goto s0
	
	State elabStoreFood{
		
		printCurrentMessage
		onMsg( storefood : storefood( TICKET, FW )) {
			[#
				val currentTime = java.time.Instant.now().epochSecond 
		   	   	val idTicket = payloadArg(0)
		   	   	val foodWeight = payloadArg(1).toInt()
		   	   
		   	   	var ticket : Ticket? = null
		   	   
		   	   	for (t in ticketList) {
					//ciclo for per trovare il biglietto del driver nella lista
		   	   	  	if(t.id == idTicket) {
		   	   	  	
		   	   	  		ticket = t
		   	   	  		break
		   	   	  	}
		   	   	}
		   	   
		   	   	if(ticket==null) { 
		   	   		println("Not found")
	   	   			#]
		   	   		replyTo storefood with storefoodrejected  : storefoodrejected(invalid)
		   	   		[#
		   	   	} else {   
			   	  	
				   	if(foodWeight<ticket!!.fw){
						currentWeightVirtual -= (ticket!!.fw-foodWeight)
				   	}
				   	   
					ticketList.remove(ticket)
				   	   
					println(currentTime - ticket!!.creationTime)
				   	   
					if((currentTime - ticket!!.creationTime) < TimeMax) {	
				   	   	println("ticket valid")	
			   	   		#]
						replyTo storefood with storefoodaccepted  : storefoodaccepted(valid)
						[#
					} else{
						currentWeightVirtual -= ticket!!.fw
						#]
						replyTo storefood with storefoodrejected  : storefoodrejected(invalid)		 	 
			  			[#
			  		}
		  		}
  			#]	  	
		}		
	}
	Goto s0
}



QActor coldstorageservice context ctxcoldstorageservice{ //Registrata a coldRoom
	
	[# var CurrentWeightReal = 0
	   var CurrentTicketFW = 0
	#]
	
	State s0 initial{
		println("coldstorageservice starts") color green
		
		delegate "newticket" to ticketservice
				
	}
	Goto waitclientrequest
	
	
	State waitclientrequest{
	  println("$name | waiting the client request...") color green
	  
	}
	Transition t0 whenRequest storefood  -> elabStoreFood
				  whenRequest clearColdRoom  -> elabClearColdRoom
	
	State elabStoreFood {
		printCurrentMessage
		onMsg( storefood : storefood( TICKET, FW )) {
			[# 
		   	   	val IDTicket = payloadArg(0)
		   	   	val FoodWeight = payloadArg(1)   	
		   	#]
		   	
		   	request ticketservice -m storefood : storefood( $IDTicket, $FoodWeight )
	   	}
	}
	Transition t1 whenReply storefoodaccepted -> elabStoreFoodAccepted
				  whenReply storefoodrejected -> elabStoreFoodRejected
				  
	
	State elabStoreFoodAccepted {
		printCurrentMessage
		replyTo storefood with storefoodaccepted  : storefoodaccepted(valid)
	} Goto waitLoadDoneRequest
	
	State elabStoreFoodRejected  {
		printCurrentMessage
		updateResource [# "ticketrejected(_)" #]
		replyTo storefood with storefoodrejected  : storefoodrejected(invalid)
	} Goto waitclientrequest
	
	
	State waitLoadDoneRequest{
		println("$name | wait load request") color green
		
	}
	Transition t2 whenRequest loaddone -> elabLoadDone
	
	State elabLoadDone {
		//ROBOT diventa occupato
		updateResource [# "robotfree(occupato)" #]
		printCurrentMessage
		println("$name | elab load done") color green
		
		onMsg( loaddone : loaddone(FW)) {
			[# 
					CurrentTicketFW = payloadArg(0).toInt()
			#]
			forward transporttrolley -m goMoveToIndoor   : goMoveToIndoor(0)
			request transporttrolley -m waitLoad : waitLoad(0)
			
		}
	}
	Transition t3 whenReply waitLoadDone -> elabChargeTaken
	
	State elabChargeTaken {
		printCurrentMessage
		println("$name | elab charge taken") color green
				
		replyTo loaddone with chargetaken : chargetaken(0)
	} Goto elabWaitDepositDone
	
	State elabWaitDepositDone {
		println("$name | elab wait load done") color green
	}
	Transition t4 whenMsg depositdone -> elabDepositDone
	
	
	State elabDepositDone {
		println("$name | elab deposit") color green
		printCurrentMessage
		[#
			CurrentWeightReal += CurrentTicketFW	
		#]
		//updateResource [# "weightupdate(12)" #] 
		updateResource [# "weightupdate($CurrentWeightReal)" #]
		//updateResource [# "depositdone($CurrentWeightReal)" #]
		//ROBOT libero
		updateResource [# "robotfree(libero)" #]
		
		println("Deposit - Current weight real: $CurrentWeightReal") color green 
	}
	Goto waitclientrequest
				  
	
	State moveRobotHome {
		
		forward transporttrolley -m goMoveToHome     : goMoveToHome(0)
		
	}
	Goto waitclientrequest
	
	State elabClearColdRoom{
		println("$name | empty coldRoom") color green
		printCurrentMessage
		forward ticketservice -m updatevirtualweight : updatevirtualweight($CurrentWeightReal)
		
		[#
			CurrentWeightReal = 0	
		#]
		updateResource [# "weightupdate($CurrentWeightReal)" #]
		updateResource [# "clearcoldroomdone($CurrentWeightReal )" #]
		println("$name | coldRoom cleared - current weight real: $CurrentWeightReal") color green
		replyTo clearColdRoom with coldRoomCleared : coldRoomCleared(0)
	
	}Goto waitclientrequest
	
	
}


//FOR IMAGE
/*
QActor serviceaccessgui context ctxserviceaccessgui{
	State s0 initial {
		println("fridgetruck starts") color red
		
		observeResource statusservice
		request coldstorageservice -m newticket		: newticket( 3 )
	}
	Transition t0 whenReply newticketaccepted -> elabTicketAccepted
	
	State elabTicketAccepted {
		
		printCurrentMessage
		onMsg( newticketaccepted : newticketaccepted(TICKET)){
			[# 
				var CurrentTicketId = payloadArg(0)
				println(CurrentTicketId)
			#]
			
			request coldstorageservice -m storefood : storefood($CurrentTicketId, 3)
		}
	}
	Transition t1 whenReply storefoodaccepted -> elabLoadDone
				  //whenReply ticketrejected -> s0
	
	State elabLoadDone {
		request coldstorageservice -m loaddone : loaddone( 3 )
	}
	Transition t1 whenReply chargetaken -> elabChargeTaken
	
	State elabChargeTaken {
		printCurrentMessage
	}
}

QActor servicestatusgui context ctxservicestatusgui{
	State s0 initial{
		
		observeResource statusservice
		
	}
} */


