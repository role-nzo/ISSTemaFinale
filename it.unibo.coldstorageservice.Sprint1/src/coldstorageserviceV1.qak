System coldstorageservice

Request engage        : engage(OWNER) //FATTO
Reply   engagedone    : engagedone(ARG) //FATTO
Reply   engagerefused : engagerefused(ARG) //FATTO

Dispatch disengage    : disengage(ARG) //FATTO

Dispatch setrobotstate: setpos(X,Y,D)
Dispatch setdirection : dir( D )  //D =up|down!left|right

Request  doplan   : doplan( PATH, OWNER, STEPTIME )
Reply doplandone  : doplandone( ARG )
Reply doplanfailed  : doplanfailed( PLANDONE, PLANTODO )


Request moverobot    :  moverobot(TARGETX, TARGETY)
Reply moverobotdone  :  moverobotok(ARG)
Reply moverobotfailed:  moverobotfailed(ARG)


Dispatch goMoveToIndoor   : goMoveToIndoor(_) //FATTO
Dispatch goMoveToColdRoom : goMoveToColdRoom(_) //FATTO
Dispatch goMoveToHome     : goMoveToHome(_) //FATTO


//Ottenimento del ticket
Request newticket		: newticket( FW )
Reply newticketaccepted : newticketaccepted( TICKET )
Reply newticketrefused  : newticketrefused( REASON )


//Show ticket per rilascio merce
Request ticketrequest : ticketrequest( TICKET, FW )
Reply ticketaccepted  : ticketaccepted( MESSAGE )
Reply ticketrejected  : ticketrejected( REASON )


Request loaddone	: loaddone(FW)
Reply chargetaken	: chargetaken(MESSAGE)
Reply chargefailure : chargefailure(REASON)


Request waitLoad : waitLoad(_)
Reply waitLoadDone : waitLoadDone(_)

Dispatch deposit : depost(_)


Context ctxbasicrobot 		  ip [host="127.0.0.1" port=8020] 
Context ctxcoldstorageservice ip [host="127.0.0.1" port=8022]
Context ctxfridgetruck 		  ip [host="127.0.0.1" port=8023] //Eliminare -> funziona solo con 3 contesti
Context ctxfridgetrucktemp 	  ip [host="localhost" port=8024] //Eliminare -> funziona solo con 3 contesti

ExternalQActor basicrobot context ctxbasicrobot

/*
 * TO-DO
 * Scrivere sullo sprint i ragionamenti fatti per raggiungere il modello
 * Scrivere perchè coldRoom POJO (non ha la necessità di inviare/ricevere messaggi direttamente)
 * Problema del load-time lungo -> perchè abbiamo scelto più messaggi per la showTicketRequest
 * 
 * PROBLEMA DRIVER MALEVOLO: come riconoscere il truck
 * 		--> non necessario
 * PROBLEMA CODA SHOW TICKET: come gestire il caso di più show ticket request
 * 		--> da testare
 * 
 * Nell'analisi del problema indicare:
 *  - Struttura ticket: idTruck,
 *  -  Perchè coldRoom come POJO e non Attore
 *  - Come abbiamo risolto problema del load time lungo
 *  - Problema carico maggiore rispetto al peso trasportabile
 * 
 */


/*
 * 
 * PROBLEMA FRIDGE TRUCK:
 * TcpMsgHandler  | ERROR: A term starting with 0-9 cannot be parsed as a number
 * dipende dal ticket che comincia con un intero
 * 		
 */
 
 
/*
 * TODO:
 * 1. gestione svuotamento periodico cold room
 * 2. gestione elaborazione ticket: 
 */

QActor transporttrolley context ctxcoldstorageservice{
	//TODO: gestire il move failed 
	
	State s0 initial {
		println("transporttrolley starts") color blue
		//Acquisizione del basicrobot
    	request basicrobot -m engage : engage(transporttrolley)	
	}
	Transition t0 whenReply engagedone    -> init
                  whenReply engagerefused -> waitrobotfree
                  
    State waitrobotfree{
		println("transporttrolley engage refused") color red
	}
	
	State init{
		println("transporttrolley init") color red
	} Goto waitcoldstorageservicerequest
	
	
	State waitcoldstorageservicerequest {
	  println("$name | waiting the client request...")
	}
	Transition t0 whenMsg goMoveToIndoor   -> elabMoveIndoor
				  whenMsg goMoveToColdRoom -> elabMoveColdRoom
				  whenMsg goMoveToHome     -> elabMoveHome
	 
	State elabMoveIndoor {	
		printCurrentMessage color blue	
		
		request basicrobot -m moverobot	: moverobot(0, 4)
	}
	Transition t0 whenReply moverobotdone -> planFinishIndoor
				  whenReply moverobotfailed -> waitcoldstorageservicerequest
	
	State elabMoveColdRoom {
		printCurrentMessage color blue
        
        request basicrobot -m moverobot	: moverobot(4, 3)	
	}
	Transition t0 whenReply moverobotdone -> planFinishColdRoom
				  whenReply moverobotfailed -> waitcoldstorageservicerequest
	
	State elabMoveHome {
		printCurrentMessage color blue

        request basicrobot -m moverobot	: moverobot(0, 0)		
	}
	Transition t0 whenReply moverobotdone -> planFinishHome
				  whenReply moverobotfailed -> waitcoldstorageservicerequest

	
	State stopped {
		
	}
	
	State resumed {
		
	}
	
	State planFinishIndoor {
		//prendere il carico
		
		println("$name | at Indoor")
		delay 3000
	} Transition t1 whenRequest waitLoad -> elabWaitLoad
	
	State planFailedIndoor {
		//prendere il carico
		
		println("$name | at Indoor")
		delay 3000
	} Transition t1 whenRequest waitLoad -> elabWaitLoad
	
	State elabWaitLoad {
		replyTo waitLoad with waitLoadDone  : waitLoadDone(valid)
	} Goto elabMoveColdRoom
	
	State planFinishColdRoom {
		//depositare il carico
		
		println("$name | at ColdRoom")
		delay 3000
		forward coldstorageservice -m deposit : deposit(0)
		
	} Goto waitcoldstorageservicerequest
	
	State planFinishHome {
		
		println("$name | at home")
		delay 1000
		
		//NON FUNZIONA
		//forward basicrobot -m setdirection : dir(down)
	    
	    
		//aspettare altre richieste
	} Goto waitcoldstorageservicerequest
		
}


QActor coldstorageservice context ctxcoldstorageservice{ //Registrata a coldRoom
	[# var ticketList = mutableListOf<Ticket>() 
	   var currentWeightVirtual = 0 
	   var currentWeightReal = 0
	   var maxWeight = 100
	   var TimeMax = 300
	   var CurrentTicketFW = 0
	   var TicketValid = false
	   
	   
	   
	#]
	//currentWeight rappresenta il peso virtuale corrente della coldRoom che considera anche i ticket ancora in coda
	
	State s0 initial{
		println("coldstorageservice starts") color green
		
		/*
		forward transporttrolley -m goMoveToIndoor   : goMoveToIndoor(0) 
		forward transporttrolley -m goMoveToColdRoom : goMoveToColdRoom(0) 
		forward transporttrolley -m goMoveToHome     : goMoveToHome(0) 
		
		forward transporttrolley -m goMoveToIndoor   : goMoveToIndoor(0) 
		forward transporttrolley -m goMoveToColdRoom : goMoveToColdRoom(0) 
		forward transporttrolley -m goMoveToHome     : goMoveToHome(0)
		*/
		
	}
	Goto waitclientrequest
	
	
	State waitclientrequest{
	  println("$name | waiting the client request...")
	}
	Transition t0 whenRequest newticket -> elabNewTicket
				  whenRequest ticketrequest  -> elabTicketRequest
				  
	
	State elabNewTicket{
		
		println("$name | elab new ticket")
		
		onMsg( newticket : newticket( FW )){
			
			[#
			
			var fw = payloadArg(0).toInt()
			if(currentWeightVirtual + fw <= maxWeight){
				currentWeightVirtual += fw
				var Id = Ticket.getRandomId() 
				var found = false
				while(found){
				found = false
				
					for (t in ticketList){
						if(t.id==Id){
							Id = Ticket.getRandomId()
							found = true
							break
						}
					}
				}
				
				var ticket = Ticket(Id, java.time.Instant.now().epochSecond, fw)
				
				ticketList.add(ticket)
				
				println(ticket)
				
			
				#] replyTo newticket with newticketaccepted:newticketaccepted( $Id ) [#
				} else {
				#] replyTo newticket with newticketrefused:newticketrefused( Peso ) [#
				}
			#]
		}
		
		
		//Verifica id univoco, il ticket verrà aggiunto ad una lista di ticket
		
		
		
	}
	Goto waitclientrequest
	
	State elabTicketRequest{
		
		println("$name | elab ticket request")
		
		onMsg( ticketrequest : ticketrequest( TICKET, FW )) {
			[#
				val currentTime = java.time.Instant.now().epochSecond 
		   	   	val idTicket = payloadArg(0)
		   	   	val foodWeight = payloadArg(1).toInt()
		   	   
		   	   	var ticket : Ticket? = null
		   	   
		   	   	for (t in ticketList) {
					//ciclo for per trovare il biglietto del driver nella lista
		   	   	  	if(t.id == idTicket) {
		   	   	  	
		   	   	  		ticket = t
		   	   	  		break
		   	   	  	}
		   	   	}
		   	   
		   	   	if(ticket==null) { 
		   	   		TicketValid = false
		   	   		println("Not found")
	   	   			#]
		   	   		replyTo ticketrequest with ticketrejected  : ticketrejected(invalid)
		   	   		[#
		   	   	} 	   
			   	  
			   	if(foodWeight<ticket!!.fw){
					currentWeightVirtual -= (ticket!!.fw-foodWeight)
			   	}
			   	   
				ticketList.remove(ticket)
			   	   
				println(currentTime - ticket!!.creationTime)
			   	   
				if((currentTime - ticket!!.creationTime) < TimeMax) {	
			   	   	
					TicketValid = true
			   	   	println("ticket valid")	
		   	   		#]
					replyTo ticketrequest with ticketaccepted  : ticketaccepted(valid)
					[#
				} else{
					currentWeightVirtual -= ticket!!.fw
			   		TicketValid = false
					#]
					replyTo ticketrequest with ticketrejected  : ticketrejected(invalid)		 
		  			[#
		  		}
  			#]	  	
		}		
	}
	Goto waitclientrequest if[# !TicketValid #] else waitLoadDoneRequest
	
	//aspetta che il truck invii loaddone per procedere
	// corretto? dovrebbe comunque partire altrimenti deadlock se il truck non invia nulla
	State waitLoadDoneRequest{
		println("$name | wait load request")
		
	}
	Transition t1 whenRequest loaddone -> elabLoadDone
	
	State elabLoadDone {
		
		println("$name | elab load done")
		
		//gestione del caso in cui il peso da depositare del truck è maggiore rispetto alla capacità massima di peso trasportabile dal trolley
		onMsg( loaddone : loaddone(FW)) {
			[# 
					CurrentTicketFW = payloadArg(0).toInt()
			#]
			forward transporttrolley -m goMoveToIndoor   : goMoveToIndoor(0)
			request transporttrolley -m waitLoad : waitLoad(0)
			//TODO: transportrolley deve andare da solo in coldRoom dopo essere andato in Indoor e prelevato
			
			
		}
	}
	Transition t2 whenReply waitLoadDone -> elabWaitDeposit
	 
	
	State elabWaitDeposit {
		//Invio charge taken fridge truck 
		println("$name | elab wait load done")
	}
	Transition t3 whenMsg deposit -> elabDeposit
	
	
	State elabDeposit {
		println("$name | elab deposit")
		
		[#
			currentWeightReal += CurrentTicketFW	
		#]
		
		println("Deposit - Current weight real: $currentWeightReal")
		
		replyTo loaddone with chargetaken : chargetaken(0) 
	}
	Transition t4 whenTime 2500 -> moveRobotHome
				  whenRequest ticketrequest  -> elabTicketRequest
				  
	
	State moveRobotHome {
		
		forward transporttrolley -m goMoveToHome     : goMoveToHome(0)
		
	}
	Goto waitclientrequest
	
	
	
}



QActor fridgetruck context ctxfridgetruck{ //SCOMPARE
	
	State s0 initial {
		println("fridgetruck starts") color red
		request coldstorageservice -m newticket		: newticket( 3 )
	}
	Transition t0 whenReply newticketaccepted -> elabTicketAccepted
	
	State elabTicketAccepted {
		
		println("QUI")
		
		onMsg( newticketaccepted : newticketaccepted(TICKET)){
			[# 
				var CurrentTicketId = payloadArg(0)
				println(CurrentTicketId)
			#]
			
			request coldstorageservice -m ticketrequest : ticketrequest($CurrentTicketId, 3)
		}
	}
	Transition t1 whenReply ticketaccepted -> elabLoadDone
				  //whenReply ticketrejected -> s0
	
	State elabLoadDone {
		request coldstorageservice -m loaddone : loaddone( 3 )
	}
	Transition t1 whenReply chargetaken -> elabChargeTaken
	
	State elabChargeTaken {
		
	}
	
}

QActor fridgetrucktemp context ctxfridgetrucktemp{ //SCOMPARE
	
	State s0 initial {
		println("fridgetruck starts") color red
		request coldstorageservice -m newticket		: newticket( 97 )
	}
	Transition t0 whenReply newticketaccepted -> elabTicketAccepted
	
	State elabTicketAccepted {
		
		println("QUI")
		
		onMsg( newticketaccepted : newticketaccepted(TICKET)){
			[# 
				var CurrentTicketId = payloadArg(0)
				println(CurrentTicketId)
			#]
			
			request coldstorageservice -m ticketrequest : ticketrequest($CurrentTicketId, 97)
		}
	}
	Transition t1 whenReply ticketaccepted -> elabLoadDone
				  //whenReply ticketrejected -> s0
	
	State elabLoadDone {
		request coldstorageservice -m loaddone : loaddone( 97 )
	}
	Transition t1 whenReply chargetaken -> elabChargeTaken
	
	State elabChargeTaken {
		
	}
	
}




